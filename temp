
output →

    ( out-of-band-record )* [ result-record ] "(gdb)" nl
result-record →

    [ token ] "^" result-class ( "," result )* nl
out-of-band-record →

    async-record | stream-record
async-record →

    exec-async-output | status-async-output | notify-async-output
exec-async-output →

    [ token ] "*" async-output nl
status-async-output →

    [ token ] "+" async-output nl
notify-async-output →

    [ token ] "=" async-output nl
async-output →

    async-class ( "," result )*
result-class →

    "done" | "running" | "connected" | "error" | "exit"
async-class →

    "stopped" | others (where others will be added depending on the needs—this is still in development).
result →

    variable "=" value
variable →

    string
value →

    const | tuple | list
const →

    c-string
tuple →

    "{}" | "{" result ( "," result )* "}"
list →

    "[]" | "[" value ( "," value )* "]" | "[" result ( "," result )* "]"
stream-record →

    console-stream-output | target-stream-output | log-stream-output
console-stream-output →

    "~" c-string nl
target-stream-output →

    "@" c-string nl
log-stream-output →

    "&" c-string nl
nl →

    CR | CR-LF
token →

    any sequence of digits. 


output_list        : %empty | output_list output;
output             : output_variant NEWLINE | error NEWLINE;
output_variant     : oob_record | result_record | OPEN_PAREN variable CLOSED_PAREN;
result_record      : opt_token CARROT result_class | opt_token CARROT result_class COMMA result_list
oob_record         : async_record | stream_record
async_record       : opt_token async_record_class async_class | opt_token async_record_class async_class COMMA result_list
async_record_class : MULT_OP | ADD_OP | EQUAL_SIGN
result_class       : STRING_LITERAL
async_class        : STRING_LITERAL
opt_variable       : %empty | variable EQUAL_SIGN
result_list         : result | result_list COMMA result
result              : opt_variable cstring | opt_variable tuple | opt_variable list
variable            : STRING_LITERAL
cstring             : CSTRING
tuple               : OPEN_BRACE CLOSED_BRACE | OPEN_BRACE result_list CLOSED_BRACE
list                : OPEN_BRACKET CLOSED_BRACKET | OPEN_BRACKET result_list CLOSED_BRACKET
stream_record       : stream_record_class cstring
stream_record_class : TILDA | AT_SYMBOL | AMPERSAND
opt_token           : %empty | token
